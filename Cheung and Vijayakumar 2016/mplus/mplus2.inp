TITLE:  Random-effects model
DATA:   FILE IS data.dat;    
VARIABLE: NAMES y v x;
        USEVARIABLES ARE y w2;  ! Use both y and w2 in the analysis

DEFINE: w2 = SQRT(v**(-1));     ! Weight for transformation           
        y = w2*y;               ! Transformed effect size

ANALYSIS:  TYPE=RANDOM;         ! Use random slopes analysis
           ESTIMATOR=ML;        ! Use ML estimation

MODEL:
        [y@0.0];                ! Intercept fixed at 0
        y@1.0;                  ! Error variance fixed at 1
        f | y ON w2;            ! f: Study specific random effects
        f*;                     ! var(f): tau^2
        [f*];                   ! mean(f): Average effect size beta_R

OUTPUT: SAMPSTAT;
        CINTERVAL(symmetric);   ! Wald CI
