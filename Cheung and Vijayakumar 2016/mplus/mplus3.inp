TITLE:  Mixed-effects model
DATA:   FILE IS data.dat;    
VARIABLE: NAMES y v x;
        USEVARIABLES ARE y x w2;! Use all variables in the analysis

DEFINE: w2 = SQRT(v**(-1));     ! Weight for transformation           
        y = w2*y;               ! Transformed effect size

ANALYSIS:  TYPE=RANDOM;         ! Use random slopes analysis
           ESTIMATOR=ML;        ! Use ML estimation

MODEL:
        [y@0.0];                ! Intercept fixed at 0
        y@1.0;                  ! Error variance fixed at 1
        f | y ON w2;            ! f: Study specific random effects
        f*;                     ! var(f): residual tau^2
        [f*];                   ! Intercept: beta_0
        f ON x;                 ! beta_1

OUTPUT: SAMPSTAT;
        CINTERVAL(symmetric);   ! Wald CI
